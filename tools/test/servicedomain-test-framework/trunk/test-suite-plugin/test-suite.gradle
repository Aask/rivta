apply plugin: 'base'
apply plugin: 'distribution'

repositories {
    mavenLocal()
}

configurations {
    support {
		transitive = false
	}
}

dependencies {
	support 'rivta.tools.test.servicedomaintestframework:soapui-support:1.0.0'
}

distributions {
    main {
        contents {
            from { configurations.support }
            from(projectDir) {
                include '**/*.xml', '**/*.docx'
                exclude 'build', '.gradle'
            }
            from(buildDir) {
                include '*.html'
            }
        }
    }
}

artifacts {
    archives distZip
}

task downloadStylesheet << {
    ant.get(src: "https://rivta.googlecode.com/svn/tools/test/servicedomain-test-framework/trunk/test-suite-plugin/ConvertToHtml.xsl",
            dest: "$buildDir")
}

task createDocumentation(dependsOn: downloadStylesheet) {
    ext.dataFile = "data.xml"
    ext.stylesheet = "$buildDir/ConvertToHtml.xsl"
    ext.outFile = "$buildDir/TestSuite-Documentation.html"
    inputs.files dataFile, stylesheet
    outputs.file outFile
    doLast {
        ant.xslt(in: dataFile,
                style: stylesheet,
                out: outFile)
    }
}

assemble.dependsOn distZip
distZip.dependsOn createDocumentation